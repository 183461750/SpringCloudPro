plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application' version '4.6.2'
}


allprojects {
    group = 'store.zabbix'
    version = 1.0
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.bmuschko.docker-spring-boot-application'

    ext {
        hutoolVersion               = '4.5.0'
        mybatisPlusVersion          = '3.1.0'
        kaptchaVersion              = '0.0.9'
        securityOauth               = '2.3.5.RELEASE'
        jasyptVersion               = '2.1.0'
        codecentricVersion          = '2.1.3'
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url "https://repo.spring.io/libs-milestone/" }
        mavenCentral()
    }


    dependencies {
        //  BOM
        implementation platform('org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE')
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE')
        // dependencies
        implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
        implementation "org.springframework.boot:spring-boot-configuration-processor"
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:${jasyptVersion}"
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "de.codecentric:spring-boot-admin-starter-client:${codecentricVersion}"
        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
        implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
        implementation 'org.projectlombok:lombok:1.18.6'
        annotationProcessor 'org.projectlombok:lombok:1.18.6'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
//        implementation 'com.google.code.gson:gson:2.8.5'
//        implementation 'com.alibaba:fastjson:1.2.56'
    }

    //排除父项目打包docker
    if (project.subprojects) {
        jar.enabled = false
        bootJar {
            enabled = false
        }
        dockerCreateDockerfile {
            enabled = false
        }
        dockerBuildImage {
            enabled = false
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}